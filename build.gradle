import java.util.regex.Pattern

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'ca.cutterslade.gradle', name: 'gradle-dependency-analyze', version: '1.2.0'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.2" apply false
}

def CHANGE_LOG_FILENAME = "CHANGELOG.md"
def projectGroup = "event-logging-transformer"

//must be applied to all projects including root
apply plugin: 'ca.cutterslade.analyze'

//if the project has a value for the passed property (i.e from the cmd line via -PpropName=xxx)
//use that, else use a default value
ext.getPropertyOrDefault = { propName, defaultValue -> 
    def val;
    if (project.hasProperty(propName) && project.getProperty(propName) != "unspecified") {
        val = project.getProperty(propName)
        println "Using propery [${propName}] with value [${val}]"
    } else {
        val = defaultValue
        println "Propery [${propName}] has no value, using default value [${val}]"
    }
    return val;
}

ext.versions = [
    //------event-logging--------------
    eventLogging: getPropertyOrDefault('version', 'SNAPSHOT'),

    //------------3rd-party------------
    jackson: '2.8.9',
    logback: '1.2.3',
    saxon: '9.6.0-6',
    slf4j: '1.7.22',
    zzDUMMYzz: 'makes sorting easier'
]


if (!(versions.eventLogging =~ /^SNAPSHOT\.*/)) {
    def changeLogFile = new File(CHANGE_LOG_FILENAME)
    def pattern = Pattern.compile(versions.eventLogging, Pattern.LITERAL)

    //This is versioned build so ensure the version is in the CHANGELOG
    if (!changeLogFile.getText("UTF-8").find(pattern)) {
        throw new GradleException("Cannot find pattern [${pattern.toString()}] in file ${CHANGE_LOG_FILENAME}, add the new version to the change log")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

allprojects {
    version versions.eventLogging
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'ca.cutterslade.analyze'
    apply plugin: 'idea'

    group "$projectGroup"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        mavenLocal()
    }

    configurations {
        all {
            exclude group: "org.slf4j", module: "slf4j-log4j12"
            exclude group: "log4j", module: "log4j"

            resolutionStrategy {
                dependencySubstitution {
                    substitute module('log4j:log4j') with module('org.slf4j:log4j-over-slf4j:$versions.slf4j')
                }

                forcedModules = [
                ]
            }
        }
    }
}

